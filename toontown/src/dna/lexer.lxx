/*
// Filename: lexer.l
// Created by:  shochet (25May00)
//
////////////////////////////////////////////////////////////////////
*/

%{
#include "lexerDefs.h"
#include "parserDefs.h"
#include "parser.h"

#include "indent.h"
#include "pnotify.h"

#include <math.h>
#include "toontownbase.h"

extern "C" int dnayywrap(void);  // declared below.

static int yyinput(void);        // declared by flex.


////////////////////////////////////////////////////////////////////
// Static variables
////////////////////////////////////////////////////////////////////

// We'll increment line_number and col_number as we parse the file, so
// that we can report the position of an error.
static int line_number = 0;
static int col_number = 0;

// current_line holds as much of the current line as will fit.  Its
// only purpose is for printing it out to report an error to the user.
static const int max_error_width = 1024;
static char current_line[max_error_width + 1];

static int error_count = 0;
static int warning_count = 0;

// This is the pointer to the current input stream.
static istream *inp = NULL;

// And this is the error stream.
static ostream *errp = NULL;

// This is the name of the dna file we're parsing.  We keep it so we
// can print it out for error messages.
static string dna_filename;

////////////////////////////////////////////////////////////////////
// Defining the interface to the lexer.
////////////////////////////////////////////////////////////////////

void
dna_init_lexer(istream &in, ostream &err, const string &filename) {
  inp = &in;
  errp = &err;
  dna_filename = filename;
  line_number = 0;
  col_number = 0;
  error_count = 0;
  warning_count = 0;
}

int
dna_error_count() {
  return error_count;
}

int
dna_warning_count() {
  return warning_count;
}


////////////////////////////////////////////////////////////////////
// Internal support functions.
////////////////////////////////////////////////////////////////////

int
dnayywrap(void) {
  return 1;
}

void
dnayyerror(const string &msg) {
  (*errp) << "\nError";
  if (!dna_filename.empty()) {
    (*errp) << " in " << dna_filename;
  }
  (*errp)
    << " at line " << line_number << ", column " << col_number << ":\n"
    << current_line << "\n";
  indent(*errp, col_number-1)
    << "^\n" << msg << "\n\n" << flush;
  error_count++;
}

void
dnayyerror(ostringstream &strm) {
  string s = strm.str();
  dnayyerror(s);
}

void
dnayywarning(const string &msg) {
  (*errp)
    << "\nWarning at line " << line_number << ", column " << col_number << ":\n"
    << current_line << "\n";
  indent(*errp, col_number-1)
    << "^\n" << msg << "\n\n" << flush;
  warning_count++;
}

void
dnayywarning(ostringstream &strm) {
  string s = strm.str();
  dnayywarning(s);
}

// Now define a function to take input from an istream instead of a
// stdio FILE pointer.  This is flex-specific.
static void
input_chars(char *buffer, int &result, int max_size) {
  nassertv(inp != NULL);
  if (*inp) {
    inp->read(buffer, max_size);
    result = inp->gcount();

    if (line_number == 0) {
      // This is a special case.  If we are reading the very first bit
      // from the stream, copy it into the current_line array.  This
      // is because the \n.* rule below, which fills current_line
      // normally, doesn't catch the first line.
      int length = min(max_error_width, result);
      strncpy(current_line, buffer, length);
      current_line[length] = '\0';
      line_number++;
      col_number = 0;

      // Truncate it at the newline.
      char *end = strchr(current_line, '\n');
      if (end != NULL) {
        *end = '\0';
      }
    }

  } else {
    // End of file or I/O error.
    result = 0;
  }
}

#undef YY_INPUT
#define YY_INPUT(buffer, result, max_size) input_chars(buffer, result, max_size)

// read_char reads and returns a single character, incrementing the
// supplied line and column numbers as appropriate.  A convenience
// function for the scanning functions below.
static int
read_char(int &line, int &col) {
  int c = yyinput();
  if (c == '\n') {
    line++;
    col = 0;
  } else {
    col++;
  }
  return c;
}

// scan_quoted_string reads a string delimited by quotation marks and
// returns it.
static string
scan_quoted_string() {
  string result;

  // We don't touch the current line number and column number during
  // scanning, so that if we detect an error while scanning the string
  // (e.g. an unterminated string), we'll report the error as
  // occurring at the start of the string, not at the end--somewhat
  // more convenient for the user.

  // Instead of adjusting the global line_number and col_number
  // variables, we'll operate on our own local variables for the
  // interim.
  int line = line_number;
  int col = col_number;

  int c;
  c = read_char(line, col);
  while (c != '"' && c != EOF) {
    result += c;
    c = read_char(line, col);
  }

  if (c == EOF) {
    dnayyerror("This quotation mark is unterminated.");
  }

  line_number = line;
  col_number = col;

  return result;
}

// eat_c_comment scans past all characters up until the first */
// encountered.
static void
eat_c_comment() {
  // As above, we'll operate on our own local copies of line_number
  // and col_number within this function.

  int line = line_number;
  int col = col_number;

  int c, last_c;

  last_c = '\0';
  c = read_char(line, col);
  while (c != EOF && !(last_c == '*' && c == '/')) {
    if (last_c == '/' && c == '*') {
      ostringstream errmsg;
      errmsg << "This comment contains a nested /* symbol at line "
             << line << ", column " << col-1 << "--possibly unclosed?"
             << ends;
      dnayywarning(errmsg);
    }
    last_c = c;
    c = read_char(line, col);
  }

  if (c == EOF) {
    dnayyerror("This comment marker is unclosed.");
  }

  line_number = line;
  col_number = col;
}



// accept() is called below as each piece is pulled off and
// accepted by the lexer; it increments the current column number.
INLINE void accept() {
  col_number += yyleng;
}

%}

HEX             0x[0-9a-fA-F]*
NUMERIC         ([+-]?(([0-9]+[.]?)|([0-9]*[.][0-9]+))([eE][+-]?[0-9]+)?)

%%

\n.* {
  // New line.  Save a copy of the line so we can print it out for the
  // benefit of the user in case we get an error.

  strncpy(current_line, yytext+1, max_error_width);
  current_line[max_error_width] = '\0';
  line_number++;
  col_number=0;

  // Return the whole line to the lexer, except the newline character,
  // which we eat.
  yyless(1);
}

[ \t\r] {
  // Eat whitespace.
  accept();
}

"//".* {
  // Eat C++-style comments.
  accept();
}

"/*" {
  // Eat C-style comments.
  accept();
  eat_c_comment();
}

[\[\],] {
  // Send square braces and commas as themselves.
  accept();
  return dnayytext[0];
}


ANIM {
  accept();
  return ANIM;
}
ANIM_BUILDING {
  accept();
  return ANIM_BUILDING;
}
ANIM_PROP {
  accept();
  return ANIM_PROP;
}
ARTICLE {
  accept();
  return ARTICLE;
}
BATTLE_CELL {
  accept();
  return BATTLE_CELL;
}
CELL_ID {
  accept();
  return CELL_ID;
}
CODE {
  accept();
  return CODE;
}
COLOR {
  accept();
  return COLOR;
}
COUNT {
  accept();
  return COUNT;
}
CORNICE {
  accept();
  return CORNICE;
}
DOOR {
  accept();
  return DOOR;
}
FLAT_BUILDING {
  accept();
  return FLAT_BUILDING;
}
FLAT_DOOR {
  accept();
  return FLAT_DOOR;
}
FRONT_DOOR_POINT {
  accept();
  return FRONT_DOOR_POINT_;
}
GROUP {
  accept();
  return DNAGROUP;
}
HEIGHT {
  accept();
  return HEIGHT;
}
HPR {
  accept();
  return HPR;
}
INTERACTIVE_PROP {
  accept();
  return INTERACTIVE_PROP;
}
LANDMARK_BUILDING {
  accept();
  return LANDMARK_BUILDING;
}
MODEL {
  accept();
  return MODEL;
}
NHPR {
  accept();
  return NHPR;
}
NODE {
  accept();
  return NODE;
}
HOOD_MODEL {
  accept();
  return HOOD_MODEL;
}
BUILDING_TYPE {
  accept();
  return BUILDING_TYPE;
}
PLACE_MODEL {
  accept();
  return PLACE_MODEL;
}
POS {
  accept();
  return POS;
}
PROP {
  accept();
  return PROP;
}
SCALE {
  accept();
  return SCALE;
}
SIGN {
  accept();
  return SIGN;
}
BASELINE {
  accept();
  return BASELINE;
}
INDENT {
  accept();
  return INDENT;
}
KERN {
  accept();
  return KERN;
}
WIGGLE {
  accept();
  return WIGGLE;
}
SIDE_DOOR_POINT {
  accept();
  return SIDE_DOOR_POINT_;
}
STREET_POINT {
  accept();
  return STREET_POINT_;
}
COGHQ_IN_POINT {
  accept();
  return COGHQ_IN_POINT_;
}
COGHQ_OUT_POINT {
  accept();
  return COGHQ_OUT_POINT_;
}
STUMBLE {
  accept();
  return STUMBLE;
}
STOMP {
  accept();
  return STOMP;
}
GRAPHIC {
  accept();
  return GRAPHIC;
}
TEXT {
  accept();
  return TEXT_;
}
LETTERS {
  accept();
  return LETTERS;
}
FLAGS {
  accept();
  return FLAGS;
}
STORE_FONT {
  accept();
  return STORE_FONT;
}
STORE_NODE {
  accept();
  return STORE_NODE;
}
STORE_SUIT_POINT {
  accept();
  return STORE_SUIT_POINT;
}
STORE_TEXTURE {
  accept();
  return STORE_TEXTURE;
}
STREET {
  accept();
  return STREET;
}
SUIT_EDGE {
  accept();
  return SUIT_EDGE;
}
TEXTURE {
  accept();
  return TEXTURE;
}
TITLE {
  accept();
  return TITLE;
}
VIS {
  accept();
  return VIS;
}
VISGROUP {
  accept();
  return VISGROUP;
}
WINDOWS {
  accept();
  return WINDOWS;
}
WALL {
  accept();
  return WALL;
}
WIDTH {
  accept();
  return WIDTH;
}

{NUMERIC} {
  // An integer or floating-point number.
  accept();
  dnayylval._number = atof(dnayytext);
  dnayylval._string = yytext;
  return NUMBER;
}

["] {
  // Quoted string.
  accept();
  dnayylval._string = scan_quoted_string();
  return STRING;
}
